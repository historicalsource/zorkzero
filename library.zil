"LIBRARY for

			       ZORK ZERO
	(c) Copyright 1988 Infocom, Inc.  All Rights Reserved."

<INCLUDE "PDEFS">

<BEGIN-SEGMENT CASTLE>

<ROOM LIBRARY
      (LOC ROOMS)
      (REGION "Flatheadia")
      (DESC "Library")
      (LDESC
"This dust-filled chamber once contained copies of every book ever written,
but all of them have been taken by the fleeing populace as reading material
for their long journeys to safe havens. The only exit is south.")
      (SOUTH TO EAST-WING)
      (OUT TO EAST-WING)
      (FLAGS RLANDBIT ONBIT)
      (SYNONYM LIBRARY)
      (MAP-LOC <PTABLE MAIN-MAP-NUM MAP-GEN-Y-4 MAP-GEN-X-8>)
      (ICON LIBRARY-ICON)
      (ACTION LIBRARY-F)
      (THINGS
 <> ( ANSWER RIDDLE)
"\"Nice try, but no help here! (signed) The Jester.\""
 FROBOZZCO ( FROBOZZCO INTERNATIONAL)
"\"FrobozzCo International is a vast conglomerate of thousands upon thousands
of companies. It can trace its origin to the Frobozz Magic Cave Company, which
was formed at the behest of King Duncanthraz in 668 GUE. Headquartered in the
400-story FrobozzCo Building in Flatheadia, FrobozzCo's corporate motto is
'You name it, we do it!'\""
( GREAT UNDERGROUND) ( HIGHWAY TOLL)
"\"The Great Underground Highway is a system of toll roads stretching
thoughout both the eastland and westland provinces.\""
<> SQUID
"\"A bottom-dwelling aquatic creature.\""
<> HELLHOUND
"\"When you spot a hellhound, run in the other direction as fast as you can!
Hellhounds are fast, fierce, and capable of devouring a human twelve times
their size in three-and-a-half seconds. They normally inhabit burnt-out or
enchanted woods and rarely venture beyond their turf, even in pursuit of
prey.\""
<> SNARFEM
"\"Snarfem, a two-player game of removing pebbles, originated in
Mithicus and comes from an ancient Mithican word meaning 'to collect
pebbles or small stones.'\""
<> FISHA
"\"Fisha is a small wand producer in Foozle, specializing in Wands of
Inanimation. Their wands tend to have a very limited life.\""
( BOZBO MUMBO ZILBO) ( BOZBO MUMBO ZILBO II III IV)
"\"One of the kings of the Entharion Dynasty.\""
<> ( BELWIT MUMBERTHRAX PHLOID)
"\"One of the kings of the Flathead Dynasty.\""
<> ( ESTUARY FROBUARY ARCH ;ORACLE MAGE JAM JELLY AUGUR SUSPENDUR
     OTTOBUR MUMBERBUR DISMEMBUR)
"\"A month of the year.\""
<> ( EASTLAND EASTLANDS)
"\"The eastlands comprise those provinces which lie on the eastern
shore of the Great Sea, such as Flatheadia and the Fublio Valley.\""
<> ( WESTLAND WESTLANDS)
"\"The westlands comprise those provinces which lie on the western
shore of the Great Sea, such as Borphee, Gurth and Frobozz.\""
 MITHICAN CHAMELEON
"\"A Mithican chameleon skin's is said to be able to imitate any color
the eye can see... and more.\""
 CURSE ( CURSE DAY)
"\"Curse Day is the anniversary of the death of Lord Dimwit Flathead the
Excessive. Some aver that, on that day in 779 GUE, a great wizard cast a
mighty Curse, and that on the 94th anniversary of that day the Empire will
collapse. Historians today dismiss this as a silly schoolyard legend.\""
<> ( ZORKMID ZORKMIDS)
"\"The Zorkmid is the unit of currency of the Great Underground Empire.\""
 ENCHANTER ( ENCHANTER GUILD)
"\"The Enchanter's Guild has several thousand members scattered in chapters
throughout the land. Amongst the more influential chapters are the ones
located in Thriff and Accardi.\""
 FROBLO PARK
"\"Froblo Park was Thomas Alva Edison's laboratory.\""
 BOSWELL BARWELL
"\"The official biographer of the Flatheads, Boswell Barwell is the author
of such notable works as THE LIVES OF THE TWELVE FLATHEADS and MUMBERTHRAX:
THE MAN BEHIND THE LEGEND.\""
<> MINX
"\"An irrestibly cuddly animal.\""
<> WINDCAT
"\"The fleetest land animal.\""
 ENDLESS FIRE
"\"The Endless Fire destroyed Mareilon in 773 GUE.\""
<> ZORK
"There's no entry for \"Zork.\" Not surprising. This is just the prequel;
it hasn't been written yet!"
<> DAVMAR
"\"Davmar was the great Thaumaturgist who discovered that the power of
magic spells could be stored on paper scrolls.\""
<> PHEEBOR
"\"Along with Borphee, Pheebor was one of the great city-states that lay at
the convergence of the Bor River and the Phee River. Borphee defeated Pheebor
in a massive battle that took place long before the age of Entharion.\""
<> ( BLOIT BLOITS)
"\"The bloit is the Empire's most common unit for measuring distances. The
bloit is defined as the distance the king's favorite pet runs in an hour.
As the discerning reader can tell from this definition, the length of the
bloit changes dramatically from ruler to ruler. (Rarely more dramatically
than in 619 GUE, when Bozbo IV -- who adored his windcat -- died, and was
succeeded by Mumbo II -- who was equally enamored of his very, very ancient
turtle). Land is usually measured in square bloits.\""
 FUBLIO VALLEY
"\"Once a richly verdant area at the southern tip of the Flathead Mountains,
the valley was defoliated in the late eighth century. For some odd reason, it
has always been a favorite spot for wizards who enjoy a hermitic lifestyle.\""
<> KORBOZ
"\"A moderately famous hermit enchanter.\""
<> GUMBOZ
"\"A very obscure hermit enchanter.\""
( FLOOD CONTROL DAM FCD) ( \#3 FCD\#3 DAM)
"The entry describes Flood Control Dam #3 as a great engineering feat of the
late 8th century, made possible by a grant of 37 million zorkmids from Dimwit
Flathead the Excessive. The dam forms a huge reservoir, and its spillover is
the source of the Frigid River. The article goes into construction techniques,
the dam's appeal as a tourist attraction, and the financial impact of the
dam's cost on the economy of the GUE."
 DIABLO MASSACRE
"\"The Diablo Massacre occured at the Zorbel Pass in 666 GUE when the invading
armies of King Duncanthrax met a native militia of trollish warriors. The
invaders were outnumbered but well-armed; the natives were equipped only with
wooden clubs and a large piece of very strong garlic. Military historians
consider the routing of the native militia as a key moment in the conquering
of the eastlands.\""
 ZORBIUS BLATTUS
"\"Greatest of the modern philosophers, Zorbius Blattus is a popular debunker
of Brogmoidism and other strange religious sects.\""
<> ( SHADOWLAND LAND SHADOW)
"\"The Shadowland (a.k.a. the Land of Shadow) is a barren area of rolling
hills, south of Flatheadia and deep underground. It lies near the point where
the Flathead Ocean's eastern shore dips underground.\""
 ROYAL MUSEUM
"\"The Royal Museum, built by Lord Dimwit Flathead in 777 GUE, houses
the crown jewels, a technology display, and a famous royal puzzle-maze.\""
 ROCKVILLE ( ESTATES CONSTRUCTION)
"\"Rockville Estates is an upscale housing complex being planned by the
Frobozz Magic Construction Company for a piece of prime cavern space on
the Great Underground Highway near Flatheadia. It was designed by the
renowned architect, Zylo Pickthorn.\""
 ZYLO PICKTHORN
"\"An architect known for his underground condominium complexes, Pickthorn
is an avid enthusiast of jousting, chess, and tiddlywinks, and has been
known to incorporate some or all of these themes into his designs.\""
 MOUNT FOOBIA
"\"Foobia is the tallest peak in the Flathead Mountains, perhaps in the entire
world. It lies toward the southern end of the range, near the Zorbel Pass, and
its apex is always hidden by clouds. It is believed that no one has ever
scaled this mighty peak.\""
 ( STEVE STEVEN) MERETZKY
"\"A mysterious mage who is said to live in the lands beyond the borders
of the world.\""
<> INFOCOM
"The entry simply reads, \"There are some things that man was not meant
to know.\""
( FRIGID RIVER) VALLEY
"\"The Frigid River Valley, a province of the Great Underground Empire,
encompasses the 15,322 square bloits which form the runoff basin of the
Frigid River.\""
 POLAR GUMFFBEAST
"\"A creature, possibly mythical, said to live in the extreme northlands
of Quendor.\""
<> DUNCANTHRAX
"\"Duncanthrax the Bellicose, the first king of the Flathead Dynasty, took
the throne from Zilbo III during a palace revolt on the last day of 659 GUE.
Duncanthrax expanded the kingdom by conquering Antharia and most of the
eastlands. He also moved the capital from Largoneth to Egreth.\""
( EGRETH CASTLE) ( EGRETH CASTLE)
"\"The Castle Egreth served as the seat of royal power from the reign of
Duncanthrax (who moved the capital from Largoneth in 660 GUE) through the
reign of Dimwit (who moved the capital to Flatheadia in 771 GUE). Egreth was,
and still is, reputed to be the most dangerous locale in the kingdom.\""
( LARGONETH CASTLE) ( CASTLE LARGONETH)
"\"The Castle Largoneth was built by Entharion the Wise back in the misty
times at the dawn of the empire. It served as the capital of the kingdom
until Duncanthrax constructed Egreth in 660 GUE. Largoneth still stands,
lonely and deserted, on the coast of Frobozz.\""
 FROBESIUS ( FROBESIUS FUBLIUS)
"\"Frobesius Fublius was a painter who specialized in rebuses. A mysterious
figure, he reputedly lived near the Zorbel Pass and vanished toward the end
of the eighth century.\""
 BORPHBELLY ( STEW INGREDIENTS)
BORPHBELLY-TEXT
<> FENSHIRE
"\"Fenshire is a swampy region which stretches east of the Flathead Mountains
to the edge of the world. The summer castle of the Flatheads is located
in a remote section of Fenshire.\""
<> CHESS
"\"An ancient game of warfare, playing on a checkered field of 64 squares.\""
<> CURSE
"According to this article, Megaboz was a mysterious wizard who lived a
hermit's life in the Fublio Valley. It is said he cast a Curse which will
someday bring down the Empire, but royal spokesmen have denied all such
rumors. Megaboz vanished in 789 GUE; some say the effort of casting the
Curse destroyed him."
<> ( TREATY ZNURG)
"\"The Treaty of Znurg, signed in 474 GUE, ended the Zucchini Wars.\""
 ZUCCHINI ( WAR WARS)
"\"The Zucchini Wars, which devastated seven provinces during the fifth
century, were finally ended by the Treaty of Znurg.\""
 RAGWEED ( BATTLE GULCH)
"\"The Battle of Ragweed Gulch, fought in 789 GUE, is most notable for the
death of Stonewall Flathead.\""
<> ( BARBEL BERKNIP BARSAP)
"\"A respected member of the Enchanters Guild.\""
 GREATER BORPHEE
"\"Borphee, a large industrial city in the westlands, is the capital of the
Greater Borphee province.\""
<> INQUISITION
"\"A crackpot religious sect; its followers believe that the Curse of Megaboz
can be forestalled by executing every person in the Empire.\""
<> ( GRUBBO GRUBBO-BY-THE-SEA ACCARDI ACCARDI-BY-THE-SEA)
"\"A small village in the westlands.\""
<> ( WISHYFOO WISHYFOOS)
"\"The Wishyfoo, who live in the vicinity of Port Foozle, alternately tell
the truth and tell a lie with every successive statement they make. Sometimes
they start with a lie, sometimes with a truth, but they always alternate
thereafter.\""
<> PREVARICON
"\"The Prevaricons, who dwell near Port Foozle, have two interesting quirks:
They always lie, and they feed unwelcome visitors to ravenous hellhounds.\""
<> ( VERITASSI VERITASS)
"\"The Veritassi, who dwell near Port Foozle, have two interesting quirks:
They never lie, and they feed unwelcome visitors to ravenous hellhounds.\""
<> MIZNIA
"\"A province at the southern fringe of the westlands, mostly jungle. Its
capital is Mizniaport.\""
<> MIZNIAPORT
"\"The capital and largest city in Miznia.\""
<> GURTH
"\"This province, which lies to the north of Miznia and Mithicus, is chiefly
woods and farmland. The Fields of Frotzen, in central Gurth, are known as the
Breadbasket of Quendor. The capital of the province is Gurth City.\""
 GURTH CITY
"\"Gurth City is the capital of Gurth province.\""
<> MITHICUS
"\"This small, mountainous province, sandwiched between Gurth and Miznia,
is a popular vacation spot.\""
<> ( FIELDS FROTZEN)
"\"The Fields of Frotzen, fertile farmland in the heart of Gurth province,
produces an annual bounty of grain and are freqently referred to as the
Breadbasket of Quendor.\""
<> FESTERON
"\"A small village in Antharia.\""
<> THRIFF
"\"The strange wandering village of Thriff has, at one time or another, been
located in most of the provinces of the westlands. Rumor has it that the
Guildmaster of Thriff's enchanters constantly moves the town in an attempt
to find a more benevolent climate for his terrible hayfever.\""
<> ENTHARION
"\"Entharion the Wise united the warring city-states of Galepath and Mareilon,
forming the kingdom of Quendor. As Quendor's first king, Entharion built the
castle Largoneth, and ruled over a kingdom which was little more than what is
currently the province of Frobozz. Our current calendar dates from the first
year of Entharion's reign.\""
<> ( GALEPATH MAREILON)
"\"Galepath and Mareilon were the two ancient cities of Quendor, which were
united by Entharion the Wise in the distant past. Mareilon was destroyed in
773 GUE by the Endless Fire.\""
<> ANTHARIA
"\"Antharia, the island province, is located in the middle of the Flathead
Ocean. It is known for its shipbuilding, marble cutting, and granola mining
industries, and is the home of Flathead Stadium. The capital is Anthar.\""
<> ANTHAR
"\"The capital of Antharia.\""
 GRANOLA ( MINES RIOTS)
"\"The granola mines in northern Antharia once supplied seemingly limitless
quantities of granola. Since the Granola Riots of 865 GUE, the causes of
which are well known, the output of the mines has fallen sharply.\""
 FLATHEAD STADIUM
"The entry calls Flathead Stadium \"The House that Babe Built,\" a tribute to
Babe Flathead's popularity as a gate attraction. Located just outside Anthar,
the stadium was supposedly large enough to hold every man, woman, and child in
Quendor."
 PORT FOOZLE
"\"Foozle, which lies several bloits west of Flatheadia, is the primary
seaport of the Frigid River Valley, and is a common departure point for
ships to Antharia.\""
<> BELBOZ
"A tiny entry mentions that Belboz is the name of a young enchanter in
the Accardi chapter of the Enchanter's Guild."
 ZORBEL PASS
"\"This wide pass through the Flathead Mountains, at the southern end of
the range, connects the Fublio and Frigid River valleys. The Zorbel Pass
was the site of the Diablo Massacre in 666 GUE.\""
<> QUENDOR
"\"See GREAT UNDERGROUND EMPIRE.\""
( GREAT UNDERGROUND) ( EMPIRE GUE)
"\"The kingdom of Quendor was renamed the Great Underground Empire by Lord
Dimwit Flathead when he became ruler in 770 GUE. It encompasses all the
lands on both sides of the Great Sea, although most of the underground
portions of the empire are in the eastlands.\""
 GREAT SEA
"\"See FLATHEAD OCEAN.\""
 ARAGAIN FALLS
"The article calls Aragain Falls the most breathtaking and awesome waterfall
in all the known lands. Found toward the northern part of the Frigid River,
the falls are a popular honeymoon spot."
 TWELVE FLATHEADS
"[Please see \"The Lives of the Twelve Flatheads Calendar\" that came in your
Zork Zero package.]")>

<CONSTANT BORPHBELLY-TEXT
"\"A popular lunchtime meal in the province of Fenshire, Borphbelly Stew
is made from fox, fowl, and earthworm. Proper preparation mandates the
simultaneous addition of the ingredients to a boiling cookpot.\"">

<ROUTINE LIBRARY-F ("OPT" (RARG <>))
	 <COND (<AND <EQUAL? .RARG ,M-BEG>
		     <EQUAL? ,PSEUDO-OBJECT ,PRSO ,PRSI>>
		<COND (<AND <VERB? RESEARCH>
			    ,LIT
			    <EQUAL? ,PRSI <> ,ENCYCLOPEDIA ,ROOMS>>
		       <SETG VOLUME-USED T>
		       ;"in DEFS2, TEST-THINGS stuffs the encyclopedia text
			 into the PSEUDO-OBJECT's ACTION property -- only
			 in this case"
		       <TELL <GETP ,PRSO ,P?ACTION> CR>)
		      (T
		       <PUTP ,PSEUDO-OBJECT ,P?ACTION <>>
		       <>)>)>>

<OBJECT ENCYCLOPEDIA
	(LOC LIBRARY)
	(DESC "Encyclopedia Frobozzica")
	(LDESC
"A copy of the Encyclopedia Frobozzica is the only book still left in the
Library, probably because it's so huge that it can't be moved except by
a team of mules.")
	(SYNONYM ENCYCLOPEDIA FROBOZZICA BOOK)
	(ADJECTIVE LARGE ENCYCLOPEDIA)
	(FLAGS READBIT VOWELBIT)
	(TEXT
"The cover reads \"Encyclopedia Frobozzica, Illustrated Edition. Copyright
882 GUE. A publication of the Frobozz Magic Encyclopedia Company.\" It would
take weeks to read the entire encyclopedia, but you could read about specific
persons or things.")
	(ACTION ENCYCLOPEDIA-F)>

<GLOBAL VOLUME-USED <>>

<ROUTINE ENCYCLOPEDIA-F ()
	 <COND (<VERB? EXAMINE>
		<TELL "The volume lies open to ">
		<COND (,VOLUME-USED
		       <TELL "a random entry." CR>)
		      (T
		       <TELL "an entry about Double Fanucci: ">
		       <PERFORM ,V?RESEARCH ,DOUBLE-FANUCCI>
		       <TELL
"   You could probably read about all sorts of other interesting people,
places, and things by looking them up in the encyclopedia." CR>)>)
	       (<AND <VERB? RESEARCH>
		     <PRSI? ,ENCYCLOPEDIA>>
		<TELL
"\"The Encyclopedia Frobozzica, a publication of the Frobozz Magic Encyclopedia
Company, is the finest of its kind in the known world. All entries are
meticulously compiled by the Frobozz Magic Encyclopedia Research Company, the
illustrations are faithfully reproduced by the Frobozz Magic Encyclopedia
Illustration Company, and the facts are all double, triple, and
quadruple-checked by the Frobozz Magic Encyclopedia Accuracy and Verification
Company. No library should be without one!\"" CR>)
	       (<VERB? OPEN>
		<TELL "It is." CR>)
	       (<VERB? CLOSE>
		<TELL "Why bother?" CR>)
	       (<VERB? TAKE>
		<TELL "Do you have a team of mules handy?" CR>)
	       (<VERB? LOOK-INSIDE>
	        <PERFORM ,V?READ ,PRSO>
		<RTRUE>)>>

<OBJECT FOUR-FLIES
	(LOC GENERIC-OBJECTS)
	(DESC "it")
	(SYNONYM FLIES FAMATHRIA)
	(ADJECTIVE FOUR FANTASTIC INT.NUM)
	(OWNER FOUR-FLIES)
	(PICTURE FOUR-FLIES-ILL)
	(RESEARCH
"The Four Fantastic Flies of Famathria, each bigger and more succulent than
the last, is a legend fabricated by a race of toad creatures who once lived
somewhere beyond the oceans of the world. Seafarers report that these toads
were ugly, cantankerous, and eternally hungry.")
	(ACTION ENC-ENTRY-F)>

<OBJECT ZILBEETHA
	(LOC GENERIC-OBJECTS)
	(SYNONYM ZILBEETHA)
        (PICTURE ZILBEETHA-ILL)
        (RESEARCH
"This is one of the oldest and dearest legends in the annals of Quendor.
Zilbeetha, a beautiful maiden, somehow angered an evil mage, and was placed
under enchantment and turned into a crystal orb on the very day that she was
to be wed. The heartbroken groom, who is always depicted holding a fragile
bloom, sought help from the wizard's goodly twin. The good wizard turned the
groom to stone, that he might stay young until the day Zilbeetha was returned
to him. The legend also has an ominous note; returning a false orb would
result in death.")
	(ACTION ENC-ENTRY-F)>

<OBJECT BROGMOIDISM
	(LOC GENERIC-OBJECTS)
	(SYNONYM BROGMOIDISM)
        (PICTURE BROGMOIDISM-ILL)
        (RESEARCH
"Brogmoidists, followers of the tenets of Brogmoidism, believe that a Great
Brogmoid supports the world upon his shoulders. The religion, which originated
sometime during the fourth century, is commonly ridiculed nowadays, and has
lost most of its adherents.")
	(ACTION ENC-ENTRY-F)>

<OBJECT FLATHEADIA
	(LOC GENERIC-OBJECTS)
	(SYNONYM FLATHEADIA)
	(PICTURE FLATHEADIA-ILL)
	(RESEARCH
"Flatheadia has been the capital of the Great Underground Empire since Dimwit
built his castle there in 770 GUE. (The former seat of royal government was
Egreth, in the Westlands.) Although still the largest population center in
the Eastlands, Flatheadia has been in a steady decline since a Curse cast by
Megaboz toward the end of the last century.")
	(ACTION ENC-ENTRY-F)>

<ROUTINE ENC-ENTRY-F ()
    <COND (<VERB? RESEARCH>
	   <COND (<AND <PRSO? ,FOUR-FLIES>
		       <ADJ-USED? ,FOUR-FLIES ,W?INT.NUM>
		       <NOT <EQUAL? ,P-NUMBER 4>>>
		  <SETG PRSO <>>
		  <RFALSE>)>
	   <PICTURED-ENTRY <GETP ,PRSO ,P?PICTURE> <GETP ,PRSO ,P?RESEARCH>>)>>

<ROUTINE PICTURED-ENTRY (ENC-PIC ENC-TEXT "OPT" (NO-WAIT <>))
	 <CLEAR -1>
	 <SCREEN ,S-FULL>
	 <SETG CURRENT-SPLIT ,ENC-PIC-LOC>
	 <DISPLAY ,ENC-BORDER 1 1>
	 <PICINF-PLUS-ONE ,ENC-PIC-LOC>
	 <DISPLAY .ENC-PIC <GET ,PICINF-TBL 0> <GET ,PICINF-TBL 1>>
	 <PICINF-PLUS-ONE ,ENC-TXT-LOC>
	 <WINPOS 3 <GET ,PICINF-TBL 0> <GET ,PICINF-TBL 1>>
	 <PICINF ,ENC-TXT-WINDOW-SIZE ,PICINF-TBL>
	 <WINSIZE 3 <GET ,PICINF-TBL 0> <GET ,PICINF-TBL 1>>
	 <WINATTR 3 15>
	 <SCREEN 3>
	 <CURSET 1 1>
	 <COLOR 1 -1>
	 <COND (.ENC-TEXT
		<TELL .ENC-TEXT>)
	       (T
		<J-ENTRY>)>
	 <COLOR 1 1>
	 <COND (.NO-WAIT
		<RTRUE>)>
	 <COND (,DEMO-VERSION?
		<INPUT-DEMO 1>)
	       (T
		<INPUT 1>)>
	 <MOUSE-INPUT?>
	 <SETG CURRENT-SPLIT ,TEXT-WINDOW-PIC-LOC>
	 <SCREEN ,S-TEXT>
	 <V-$REFRESH>
	 <RTRUE>>

<OBJECT OTHER-FLATHEADS
	(DESC "member of the Flathead family")
	(SYNONYM FLATHEAD)
	(ADJECTIVE MICHAEL WURB KING OLIVER WENDELL)
	(ACTION OTHER-FLATHEADS-F)>

<ROUTINE OTHER-FLATHEADS-F ()
	 <COND (<VERB? RESEARCH>
		<COND (<ADJ-USED? ,OTHER-FLATHEADS ,W?MICHAEL>
		       <TELL
"\"A popular musician, formerly of the Flathead Five.\"" CR>)
		      (<ADJ-USED? ,OTHER-FLATHEADS ,W?KING ,W?WURB>
		       <TELL
"\"Wurb Flathead, son of Idwit Oogle Flathead, is the current ruler of the
Great Underground Empire. The twelfth king in the Flathead dynasty, Wurb
assumed the throne in 881 GUE.\"" CR>)
		      (<ADJ-USED? ,OTHER-FLATHEADS ,W?OLIVER ,W?WENDELL>
		       <TELL "\"A noted judge.\"" CR>)
		      (T
		       <PERFORM ,V?RESEARCH ,BABE-PORTRAIT>
		       <RTRUE>)>)>>

<OBJECT SAINTS
	(DESC "saint")
	(SYNONYM SAINT ST BALHU HONKO QUAKKO BOVUS WISKUS)
	(ADJECTIVE SAINT ST)
	(FLAGS NDESCBIT VOWELBIT)
	(ACTION SAINTS-F)>

<ROUTINE SAINTS-F ()
	 <COND (<VERB? RESEARCH>
		<TELL "\"The patron saint of ">
		<COND (<NOUN-USED? ,SAINTS ,W?BALHU>
		       <TELL "circus performers">)
		      (<NOUN-USED? ,SAINTS ,W?HONKO>
		       <TELL "people who play very odd musical instruments">)
		      (<NOUN-USED? ,SAINTS ,W?QUAKKO>
		       <TELL "people who aren't sure of things">)
		      (<NOUN-USED? ,SAINTS ,W?WISKUS>
		       <TELL "all those who raise meat animals">)
		      (T ;Wiskus
		       <TELL "those who design fine slate patios">)>
		<TELL ".\"" CR>)>>

<OBJECT WIZARD-OF-FROBOZZ
	(DESC "Wiazard of Frobozz")
	(SYNONYM WIZARD FROBOZZ)
	(OWNER WIZARD-OF-FROBOZZ)
	(ACTION WIZARD-OF-FROBOZZ-F)>

<ROUTINE WIZARD-OF-FROBOZZ-F ()
	 <COND (<VERB? RESEARCH>
		<COND (<NP-OF <GET-NP ,WIZARD-OF-FROBOZZ>>
		       ;"input was READ ABOUT WIZARD OF FROBOZZ"
		       <TELL
"\"A former member of the Circle of Enchanters, the Wizard of Frobozz was
removed for forgetfulness bordering on senility. Among his other failings,
he developed an inability to cast any spells other than those beginning with
the letter 'F'. He was banished to an obscure corner of the Empire after
he accidentally turned the entire West Wing of Dimwit Flathead's castle into
a mountain of Fudge.\"" CR>)
		      (T
		       ;"input was READ ABOUT FROBOZZ"
		       <TELL
"\"An ancient province in the northern part of the westlands, Frobozz
is the site of many historic sites such as Galepath, Mareilon, and the
Castle Largoneth.\"" CR>)>)>>

<OBJECT ARMOR
	(LOC LIBRARY)
	(OWNER ARMOR)
	(DESC "suit of armor")
	(SYNONYM SUIT ARMOR ARMOUR)
	(FLAGS TRYTAKEBIT)
	(ACTION ARMOR-F)>

<GLOBAL ARMOR-TOUCH 0>

<ROUTINE ARMOR-F ()
	 <COND (<VERB? TOUCH>
		<SETG ARMOR-TOUCH <+ ,ARMOR-TOUCH 1>>
		<COND (<EQUAL? ,ARMOR-TOUCH 3>
		       <COND (<IN? ,SCROLL ,LOCAL-GLOBALS>
			      ;"don't bother having jester deliver it"
			      <REMOVE ,SCROLL>)>
		       <MOVE ,LANCE ,HERE>
		       <TELL "The armor opens and a lance falls out!" CR>
		       <INC-SCORE 12>)
		      (<EQUAL? ,ARMOR-TOUCH 1 2>
		       <TELL "It ">
		       <COND (<EQUAL? ,ARMOR-TOUCH 2>
			      <TELL "still ">)>
		       <TELL "feels like metal." CR>)
		      (T
		       <RFALSE>)>)
	       (<VERB? TAKE>
		<TELL
"The armor is permanently mounted as part of the library decor." CR>)
	       (<VERB? ENTER>
		<TELL
"The armor was made for a much shorter person -- or at least a person with
a much flatter head." CR>)
	       (<VERB? EXAMINE>
		<TELL
"This battle-scarred armor is the sort that was worn around the time of
the Battle of Ragweed Gulch." CR>)>>

<OBJECT LANCE
	(DESC "lance")
	(SYNONYM LANCE)
	(FLAGS TAKEBIT MAGICBIT)
	(SIZE 10)
	(VALUE 0) ;"12 points given elsewhere">

<END-SEGMENT>